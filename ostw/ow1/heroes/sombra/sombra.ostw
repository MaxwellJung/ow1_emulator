rule: "[sombra.opy]: Initialize Sombra"
Event.OngoingPlayer
Player.Sombra
# without this flag, the reset code in generic.opy executes after initialization
if (call_init == true)
{
    AllPlayers(TeamOf(EventPlayer())).friendly_sombra_player = EventPlayer();
    AllPlayers(OppositeTeamOf(TeamOf(EventPlayer()))).enemy_sombra_player = EventPlayer();
}

rule: "[sombra.opy]: Cancel invis if hack activated"
Event.OngoingPlayer
Player.Sombra
if (IsUsingAbility1(EventPlayer()) == true)
if (IsFiringSecondary(EventPlayer()) == true)
{
    PressButton(EventPlayer(), Button.Ability1);
}

rule: "[sombra.opy]: OW1 hack duration on enemies"
Event.OngoingPlayer
if (HasStatus(EventPlayer(), Status.Hacked) == true)
{
    SetStatus(EventPlayer(), null, Status.Hacked, 5);
}

rule: "[sombra.opy]: No damage with EMP"
Event.OngoingPlayer
Player.Sombra
if (UltimateChargePercent(EventPlayer()) >= 100)
{
    WaitUntil(IsButtonHeld(EventPlayer(), Button.Ultimate), 999999986991104);
    WaitUntil(UltimateChargePercent(EventPlayer()) <= 0, 999999986991104);
    SetDamageDealt(EventPlayer(), 0);
    WaitUntil(IsUsingUltimate(EventPlayer()), 999999986991104);
    SetDamageDealt(EventPlayer(), 100);
}

rule: "[sombra.opy]: Deal OW1 machine pistol damage"
Event.OnDamageDealt
Player.Sombra
if (EventAbility() == Button.PrimaryFire)
{
    Damage(Victim(), Attacker(), EventDamage() * 1.067 - EventDamage());
}

rule: "[sombra.opy]: Remove hack bonus damage"
Event.OngoingPlayer
if (HasStatus(EventPlayer(), Status.Hacked) == true)
{
    StartDamageModification(EventPlayer(), enemy_sombra_player, 80, DamageModificationRev.None);
    hack_damage_modifier = LastDamageModificationID();
    WaitUntil(!HasStatus(EventPlayer(), Status.Hacked), 999999986991104);
    StopDamageModification(hack_damage_modifier);
}

rule: "[sombra.opy]: Increase stealth movement speed"
Event.OngoingPlayer
Player.Sombra
if (IsUsingAbility1(EventPlayer()) == true)
{
    SetMoveSpeed(EventPlayer(), 103.125);
    WaitUntil(!IsUsingAbility1(EventPlayer()), 999999986991104);
    SetMoveSpeed(EventPlayer(), 100);
}

rule: "[sombra.opy]: Set default hack cooldown"
Event.OngoingPlayer
Player.Sombra
if (AbilityCooldown(EventPlayer(), Button.SecondaryFire) >= 4)
{
    SetAbilityCooldown(EventPlayer(), Button.SecondaryFire, 8);
}
